{
  parserClass="com.github.alaanor.candid.CandidParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Candid"
  psiImplClassSuffix="Impl"
  psiPackage="com.github.alaanor.candid.psi"
  psiImplPackage="com.github.alaanor.candid.psi.impl"

  elementTypeHolderClass="com.github.alaanor.candid.CandidTypes"
  elementTypeClass="com.github.alaanor.candid.psi.CandidElementType"
  tokenTypeClass="com.github.alaanor.candid.psi.CandidTokenType"

  tokens = [
    LINE_COMMENT = 'LINE_COMMENT'
    BLOCK_COMMENT = 'BLOCK_COMMENT'
    OP_EQ = "="
    SEMICOLON = ";"
    COMMA = ","
    COLON = ":"
    LBRACE = "{"
    RBRACE = "}"
    LPAREN = "("
    RPAREN = ")"
    LARROW = "->"
    UNDERSCORE = "_"
    DOUBLE_QUOTE = '"'
  ]
}

candidFile ::= prog

// meta

private meta list_item ::= <<p>> (<<s>> | !<<p>>) { pin=1 }
private meta list ::= <<list_item <<p>> <<s>>>>*

// candid grammar

private prog ::= <<list (definition|import_statement) ';'>> actor?

definition ::= type identifier_declaration '=' data_type {
    pin=1
    name="type"
}

actor ::= service id? ':' (tup_type '->')? (actor_type | identifier_reference) { pin=1 name="service" }
private actor_type ::= '{' <<list meth_type ';'>>? '}' { pin=1 }

import_statement ::= import string_literal {
    mixin="com.github.alaanor.candid.psi.mixin.CandidImportStatementMixin"
    name="import"
    pin=1
}

private meth_type ::= name ':' (func_type | identifier_reference)
private func_type ::= tup_type '->' tup_type func_annotation*
private func_annotation ::= oneway | query
tup_type ::= '(' <<list arg_type ",">>? ')' { pin=1 }
private arg_type ::= data_type | name ':' data_type

private field_type ::= nat_type ':' data_type | name ':' data_type { pin=2 }
private field_type_record ::= field_type | data_type
private field_type_variant ::= field_type | nat_type | name

data_type ::= identifier_reference  | comp_type | prim_type
private comp_type ::= cons_type | ref_type
private prim_type ::= num_type | bool | text | null | reserved | empty | principal
private num_type ::= nat | nat8 | nat16 | nat32 | nat64
    | int | int8 | int16 | int32 | int64
    | float32 | float64

private cons_type ::= opt_statement | vec_statement | record_statement | variant_statement | blob

private opt_statement ::= opt data_type { pin=1 name="opt" }
private vec_statement ::= vec data_type { pin=1 name="vec" }
record_statement ::= record '{' <<list field_type_record ';'>> '}' { pin=1 name="record" }
variant_statement ::= variant '{' <<list field_type_variant ';'>> '}' { pin=1 name="variant" }

private ref_type ::= func_statement | service_statement
private func_statement ::= func func_type { pin=1 name="func" }
private service_statement ::= service actor_type { pin=1 name="service" }

private nat_type ::= num | hex_0x { name="number" }
private num ::= digit('_'? digit)*
private name ::= id | string_literal
string_literal ::= '"'char*'"' {
    mixin="com.github.alaanor.candid.psi.mixin.CandidStringLiteralMixin"
    name="string"
}
private char ::= ascii | utf8enc | hex_escape_short | hex_escape_long | escape

identifier_declaration ::= id {
    implements="com.github.alaanor.candid.psi.primitive.CandidNameIdentifierOwner"
    mixin="com.github.alaanor.candid.psi.mixin.CandidIdentifierDeclarationMixin"
    name="identifier"
}

identifier_reference ::= id {
    mixin="com.github.alaanor.candid.psi.mixin.CandidIdentifierReferenceMixin"
    name = "identifier"
}